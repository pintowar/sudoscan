plugins {
    id "idea"
    id "com.limark.gitflowsemver" version "0.3.1"
    id "org.jetbrains.kotlin.jvm" version "1.4.10" apply false
    id "org.jetbrains.kotlin.kapt" version "1.4.10" apply false
    id "org.jetbrains.dokka" version "1.4.10" apply false
    id "org.bytedeco.gradle-javacpp-platform" version "1.5.4" apply false
}

allprojects {
    group = "com.github.pintowar"
//    version = "0.3.0"

    ext {
        vPicocli = "4.5.1"
        vNd4j = "1.0.0-beta7"
        vJavaCv = "1.5.3"
//        javacppPlatform = "linux-x86_64,macosx-x86_64,windows-x86_64"
    }

    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "org.bytedeco.gradle-javacpp-platform"

    repositories {
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    dependencies {
        implementation platform("org.jetbrains.kotlin:kotlin-bom")
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        implementation "io.github.microutils:kotlin-logging:1.8.3"
        runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

        testImplementation "io.kotest:kotest-runner-junit5:4.2.2"
        testImplementation "org.jetbrains.kotlin:kotlin-reflect"
        testImplementation "io.mockk:mockk:1.9"
    }

    test {
        useJUnitPlatform()
    }
}

task assembleDesktopApp(dependsOn: [':sudoscan-deskapp:shadowJar']) {
    group = 'build'
    description = 'Build desktop app'

    doLast {
        copy {
            from fileTree(dir: "${project(':sudoscan-deskapp').buildDir}/libs/")
            into "$rootDir/build/"
            logger.quiet "JAR generated at $rootDir/build/."
        }
    }
}
