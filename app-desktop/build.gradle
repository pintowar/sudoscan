plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

apply plugin: "org.jetbrains.kotlin.kapt"

kapt {
    arguments {
        arg("project", "${project.group}/${project.name}")
    }
}

dependencies {

    implementation project(":core")

    kapt "info.picocli:picocli-codegen:$vPicocli"
    implementation "info.picocli:picocli:$vPicocli"
    implementation "org.beryx:awt-color-factory:1.0.1"

    api("org.datavec:datavec-data-image:$vNd4j") {

        exclude(group: "org.freemarker")
        exclude(group: "com.github.jai-imageio")
        exclude(group: "com.twelvemonkeys.imageio", module: "imageio-bmp")
        exclude(group: "com.twelvemonkeys.imageio", module: "imageio-psd")
        exclude(group: "com.twelvemonkeys.imageio", module: "imageio-tiff")

        ["artoolkitplus", "flandmark", "flycapture", "leptonica", "libdc1394",
         "libfreenect", "libfreenect2", "librealsense", "librealsense2",
         "tesseract", "videoinput", "javacv-platform"].each {
            exclude(group: "org.bytedeco", module: "$it")
        }
    }

}

application {
    // Define the main class for the application.
    mainClassName = "com.github.pintowar.sudoscan.viewer.SudoscanApplicationKt"
}

shadowJar {
    mainClassName = "com.github.pintowar.sudoscan.viewer.SudoscanApplicationKt"
    archiveBaseName = "sudoscan-desktop-app"
    mergeServiceFiles()
}
